buildscript {
  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath "org.xtext:xtext-gradle-plugin:1.0.17"
  }
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'eclipse'
apply plugin: "org.xtext.xtend"



xtend {
  generator{
    javaSourceLevel = 1.8
  }
}

uploadArchives {
  repositories.mavenDeployer {
    repository(url: "file:///${System.properties['user.home']}/artifacts/")
    pom.version = "2.4.11"
    pom.artifactId = "rejectfree"
    pom.groupId = "ca.ubc.stat"
  }
}

eclipse { 
  classpath {
    defaultOutputDir = file('bin')
  }
}

repositories {
  mavenCentral()
  jcenter()
  maven {
    url "https://www.stat.ubc.ca/~bouchard/maven/"
  }
}

dependencies {
  compile group: 'com.google.guava', name: 'guava', version: '18.0'
  testCompile group: 'junit', name: 'junit', version: '4.12'
  compile group: 'junit', name: 'junit', version: '4.12'
  compile group: 'ca.ubc.stat', name: 'tutorialj', version: '2.0.4'
  compile group: 'ca.ubc.stat', name: 'binc', version: '1.2.1'
  compile group: 'ca.ubc.stat', name: 'briefj', version: '2.4.20'
  compile group: 'ca.ubc.stat', name: 'conifer', version: '1.3.6'
  compile group: 'ca.ubc.stat', name: 'xlinear', version: '2.0.0'
  compile group: 'ca.ubc.stat', name: 'inits', version: '2.6.0'
  compile 'org.eclipse.xtend:org.eclipse.xtend.lib:2.11.0'
  compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.4'
  compile 'com.google.code.gson:gson:2.3.1'
  compile 'org.jblas:jblas:1.2.3'
  compile group: 'ca.ubc.stat', name: 'bayonet', version: '2.3.27'
  compile 'com.googlecode.efficient-java-matrix-library:ejml:0.24'
  compile group: 'ca.ubc', name: 'ahmc-java', version: '1.0.1'
  compile 'net.sourceforge.parallelcolt:parallelcolt:0.10.0'
//  compile group: 'org.apache.spark', name: 'spark-core_2.11', version: '2.1.0'
//  compile group: 'org.apache.spark', name: 'spark-sql_2.10', version: '2.1.0'
}



apply plugin: 'versions'

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}


buildscript {
  repositories {
    maven { url "https://github.com/ben-manes/gradle-versions-plugin/raw/mvnrepo" }
    mavenCentral()
  }

  dependencies {
    classpath 'com.github.ben-manes:gradle-versions-plugin:0.5-beta-1'
  }
}

if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        //noinspection SpellCheckingInspection
        tasks.withType(Javadoc) {
            // disable the crazy super-strict doclint tool in Java 8
            //noinspection SpellCheckingInspection
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

// http://www.gradle.org/docs/current/userguide/application_plugin.html
// use 'gradle startScripts' to create script
// use 'gradle distZip' to create deployable zip
// use 'gradle installApp' to copy it somewhere
apply plugin:'application'

configurations.archives.with {
    artifacts.remove artifacts.find { it.archiveTask.is distZip }
    artifacts.remove artifacts.find { it.archiveTask.is distTar }
}  

// Creates scripts for entry points
// Subproject must apply application plugin to be able to call this method.
def createScript(project, mainClass, name) {
  project.tasks.create(name: name, type: CreateStartScripts) {
    outputDir       = new File(project.buildDir, 'scripts')
    mainClassName   = mainClass
    applicationName = name
    classpath       = project.tasks[JavaPlugin.JAR_TASK_NAME].outputs.files + project.configurations.runtime
  }
  project.tasks[name].dependsOn(project.jar)

  project.applicationDistribution.with {
    into("bin") {
      from(project.tasks[name])
      fileMode = 0755
    }
  }
}

// The next two lines disable the tasks for the primary main which by default
// generates a script with a name matching the project name. 
// You can leave them enabled but if so you'll need to define mainClassName
// And you'll be creating your application scripts two different ways which 
// could lead to confusion
startScripts.enabled = false
run.enabled = false

// Call this for each Main class you want to expose with an app script
createScript(project, 'ca.ubc.bps.factory.BPSFactory',                       'bps-run')
createScript(project, 'ca.ubc.bps.processors.loaders.WriteGlobalTrajectory', 'bps-toGlobalTraj')
